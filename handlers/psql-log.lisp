(in-package :thune)

(defclass log-msg ()
  ((msgid :col-type serial :reader msgid)
   (received :col-type simple-date:timestamp :initarg :received :reader received)
   (nick :col-type string :initarg :nick :reader nick)
   (username :col-type string :initarg :username :reader username)
   (host :col-type string :initarg :host :reader host)
   (command :col-type string :initarg :command :reader command)
   (channel :col-type string :initarg :channel :reader channel)
   (message :col-type string :initarg :message :reader message))
  (:metaclass postmodern:dao-class)
  (:keys msgid))

(defhandler psql-log (channel message)
  (declare (ignore channel))
  (unless postmodern:*database*
    (postmodern:connect-toplevel (conf-value 'database)
                                 (conf-value 'db-user)
                                 (conf-value 'db-password)
                                 (conf-value 'db-host)))
  (when (and (typep (prefix message) 'user)
             (some (lambda (x) (string-equal (command message) x))
                   (list "PRIVMSG" "NOTICE" "PART")))
    (postmodern:insert-dao (make-instance 'log-msg
                                          :received (simple-date:universal-time-to-timestamp (received message))
                                          :nick (nick (prefix message))
                                          :username (username (prefix message))
                                          :host (host (prefix message))
                                          :command (command message)
                                          :channel (first (parameters message))
                                          :message (second (parameters message))))))